
////////////////////

// add a line about villagers to Captain Arat's dialogue

<<<<<<<< d5/kpcapt+.d
APPEND KPCAPT01
 IF ~~ THEN BEGIN d5_new_state
  SAY ~Many local villagers had taken refuge in the Keep when the troll attack began; unfortunately that left them vulnerable to capture when the yuan-ti tunnelers sudden appeared inside the castle.~
  IF ~~ THEN GOTO 4
 END
END

EXTEND_BOTTOM KPCAPT01 3 
 IF ~~ THEN GOTO d5_new_state
END
>>>>>>>> 

// or this... but not sure how to use it
// ALTER_TRANS KCAPT01 BEGIN 3 END BEGIN 0 END BEGIN "EPILOGUE" ~GOTO d5_new_state~ END

COPY ~d5/kpcapt+.d~ ~weidu_external/%MOD_FOLDER%/compile/kpcapt+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/kpcapt+.d~

////////////////////

// add travel triggers between de'arnise keep and dragon's eye

COPY_EXISTING ~AR1301.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 290  // x coordinate
    fj_box_top      = 990 // y coordinate
    fj_box_right    = 550 // x coordinate
    fj_box_bottom   = 1080 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 290 + (990 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 550 + (990 << 16)
    fj_vertex_2     = 550 + (1080 << 16)
    fj_vertex_3     = 290 + (1080 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix4001 //name of this travel region so you can address it via script etc.
    fj_destination_area = ID4001 //name of the destination area
    fj_destination_name = FR4000 //name of exitpoint in destination area
  END

/* add exitpoint for backwards travel */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 760 //x coordinate where the group will spawn
    fj_loc_y       = 1235 //y coordinate where the group will spawn
    fj_orientation = 13   //ESE - face direction in which group is looking after coming through
    STR_VAR
    fj_structure_type = entrance
    fj_name           = d5id1301 //name of this new exitpoint so it can be referenced for the travel region of the other area
  END
BUT_ONLY 

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4001.are~) BEGIN
 COPY_EXISTING ~ID4001.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 10  // x coordinate
    fj_box_top      = 1080 // y coordinate
    fj_box_right    = 300 // x coordinate
    fj_box_bottom   = 1300 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 10 + (1080 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 300 + (1080 << 16)
    fj_vertex_2     = 300 + (1300 << 16)
    fj_vertex_3     = 10 + (1300 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix1301 //name of this travel region so you can address it via script etc.
    fj_destination_area = AR1301 //name of the destination area
    fj_destination_name = d5id1301 //name of exitpoint in destination area
  END
/* don't think this is needed?
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 345 //x coordinate where the group will spawn
    fj_loc_y       = 1200 //y coordinate where the group will spawn
    fj_orientation = 12   //E - face direction in which group is looking after coming through
    STR_VAR
    fj_structure_type = entrance
    fj_name           = d5id1301 //name of this new exitpoint so it can be referenced for the travel region of the other area
  END
*/
 BUT_ONLY
END 

////////////////////

// mark real IWD vs IWD in EET

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__iwd_real.d5~) BEGIN
  
  ACTION_IF (FILE_EXISTS_IN_GAME ~ID1000.are~) BEGIN
<<<<<<<< d5/id1000_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("D5_IWD_REAL","GLOBAL",1)
	SetGlobal("D5_EET_IWD","GLOBAL",0)
END
>>>>>>>> 
    COPY ~d5/id1000_.baf~ ~weidu_external/%MOD_FOLDER%/compile/id1000_.baf~
    LAF cd_extend_bg_area_script STR_VAR area = ~id1000~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/id1000_~ END
  END

  COPY_EXISTING ~sw1h04.itm~ ~override/d5__iwd_real.d5~

END

////////////////////

// set up scripts to make dragon's eye travel triggers reactive to where the player is

ACTION_IF (FILE_EXISTS_IN_GAME ~AR1301.are~) BEGIN
<<<<<<<< d5/ar1301_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",0)
	Global("D5_EET_IWD","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("D5_EET_IWD","GLOBAL",1)
	TriggerActivation("d5ix4000",TRUE)
END

IF
	Global("D5_IWD_REAL","GLOBAL",1)
	Global("D5TRAVEL","AR1301",0)
THEN
	RESPONSE #100
	SetGlobal("D5TRAVEL","AR1301",1)
	TriggerActivation("d5ix4000",FALSE)
END
>>>>>>>> 
  COPY ~d5/ar1301_.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar1301_.baf~
  LAF cd_extend_bg_area_script STR_VAR area = ~ar1301~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/ar1301_~ END
END

COPY_EXISTING ~cfcapvil.cre~ ~override~
  WRITE_ASCII 0x280 ~XP_VILLA~ #32
IF_EXISTS

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4001.are~) BEGIN
<<<<<<<< d5/id4001_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",1)
	Global("D5_TRA_IWD","ID4001",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4001",1)
	SetGlobal("D5_TRA_BG2","ID4001",0)
	TriggerActivation("To4000",TRUE)
	TriggerActivation("d5ix1301",FALSE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("D5_TRA_BG2","ID4001",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4001",0)
	SetGlobal("D5_TRA_BG2","ID4001",1)
	TriggerActivation("To4000",FALSE)
	TriggerActivation("d5ix1301",TRUE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("Villager_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("Villager_Gone","GLOBAL",1)
	ActionOverride("XP_VILLA",DestroySelf())
END
>>>>>>>> 
  COPY ~d5/id4001_.baf~ ~weidu_external/%MOD_FOLDER%/compile/id4001_.baf~
  LAF cd_extend_bg_area_script STR_VAR area = ~id4001~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/id4001_~ END
END

////////////////////

// selectively skip presio's level

// and other scripted changes to ID4002/4004

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4002.are~) BEGIN
 COPY_EXISTING ~ID4002.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 2810  // x coordinate
    fj_box_top      = 2075 // y coordinate
    fj_box_right    = 3000 // x coordinate
    fj_box_bottom   = 2220 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 2810 + (2075 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 3000 + (2075 << 16)
    fj_vertex_2     = 3000 + (2220 << 16)
    fj_vertex_3     = 2010 + (2220 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix4004 //name of this travel region so you can address it via script etc.
    fj_destination_area = ID4004 //name of the destination area
    fj_destination_name = FR4003 //name of exitpoint in destination area
  END
 BUT_ONLY
END 

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4004.are~) BEGIN
 COPY_EXISTING ~ID4004.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 1  // x coordinate
    fj_box_top      = 1860 // y coordinate
    fj_box_right    = 240 // x coordinate
    fj_box_bottom   = 2120 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 1 + (1860 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 240 + (1860 << 16)
    fj_vertex_2     = 240 + (2120 << 16)
    fj_vertex_3     = 1 + (2120 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix4002 //name of this travel region so you can address it via script etc.
    fj_destination_area = ID4002 //name of the destination area
    fj_destination_name = FR4003 //name of exitpoint in destination area
  END
 BUT_ONLY
END 

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4002.bcs~) BEGIN
<<<<<<<< d5/id4002_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",1)
	Global("D5_TRA_IWD","ID4002",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4002",1)
	SetGlobal("D5_TRA_BG2","ID4002",0)
	TriggerActivation("To4003",TRUE)
	TriggerActivation("d5ix4004",FALSE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("D5_TRA_BG2","ID4002",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4002",0)
	SetGlobal("D5_TRA_BG2","ID4002",1)
	TriggerActivation("To4003",FALSE)
	TriggerActivation("d5ix4004",TRUE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("Egenia_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("Egenia_Gone","GLOBAL",1)
	ActionOverride("egenia",DestroySelf())
END
>>>>>>>> 
  COPY ~d5/id4002_.baf~ ~weidu_external/%MOD_FOLDER%/compile/id4002_.baf~
  LAF cd_extend_bg_area_script STR_VAR area = ~id4002~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/id4002_~ END
END

COPY_EXISTING ~egenia.cre~ ~override~
  WRITE_ASCII 0x280 ~egenia~ #32
IF_EXISTS

ACTION_IF (FILE_EXISTS_IN_GAME ~ID4004.bcs~) BEGIN
<<<<<<<< d5/id4004_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",1)
	Global("D5_TRA_IWD","ID4004",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4004",1)
	SetGlobal("D5_TRA_BG2","ID4004",0)
	TriggerActivation("To4003",TRUE)
	TriggerActivation("d5ix4002",FALSE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("D5_TRA_BG2","ID4004",0)
THEN
	RESPONSE #100
	SetGlobal("D5_TRA_IWD","ID4004",0)
	SetGlobal("D5_TRA_BG2","ID4004",1)
	TriggerActivation("To4003",FALSE)
	TriggerActivation("d5ix4002",TRUE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("MWater_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("MWater_Gone","GLOBAL",1)
	ActionOverride("mwater",DestroySelf())
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("CKnight_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("CKnight_Gone","GLOBAL",1)
	ActionOverride("cknight",DestroySelf())
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("IQuinva_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("IQuinva_Gone","GLOBAL",1)
	ActionOverride("iquinva",DestroySelf())
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("RCopper_Gone","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("RCopper_Gone","GLOBAL",1)
	ActionOverride("rcopper",DestroySelf())
END
>>>>>>>> 
  COPY ~d5/id4004_.baf~ ~weidu_external/%MOD_FOLDER%/compile/id4004_.baf~
  LAF cd_extend_bg_area_script STR_VAR area = ~id4004~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/id4004_~ END
END

COPY_EXISTING ~mwater.cre~ ~override~
  WRITE_ASCII 0x280 ~mwater~ #32
IF_EXISTS

COPY_EXISTING ~cknight.cre~ ~override~
  WRITE_ASCII 0x280 ~cknight~ #32
IF_EXISTS

COPY_EXISTING ~iquinva.cre~ ~override~
  WRITE_ASCII 0x280 ~iquinva~ #32
IF_EXISTS

COPY_EXISTING ~rcopper.cre~ ~override~
  WRITE_ASCII 0x280 ~rcopper~ #32
IF_EXISTS

////////////////////

// change Albion & Yxunomei's dialogue

WITH_TRA ~iwd1_eet/lang/%LANGUAGE%/dialog.tra~ BEGIN
  COMPILE ~%MOD_FOLDER%/data/compile/dalbion.d~
END

<<<<<<<< d5/dyxun+.d
ADD_STATE_TRIGGER DYXUN 0 ~Global("D5_IWD_REAL","GLOBAL",1)~

APPEND DYXUN
IF ~Global("D5_IWD_REAL","GLOBAL",0)~ THEN BEGIN d5yxun_0 
  SAY ~So. It has come to this, has it? I did not expect a group of natives to break the power of my minions.~
  IF ~~ THEN REPLY ~Minions? You mean the snake people?~ GOTO d5yxun_1
  IF ~~ THEN REPLY ~Are you the one responsible for everything going on in D'Arnise Keep? The abductions, the trolls, the yuan-ti?~ GOTO d5yxun_3
  IF ~~ THEN REPLY ~I am here to put a stop to your agenda, whatever it is, by any means necessary.~ GOTO d5yxun_7
END

IF ~~ THEN BEGIN d5yxun_1
  SAY ~They are not "snake people," you ignorant pig. They are yuan-ti, faithful servants and worthy heirs to the kingdom I shall leave behind once my business is finished.~
  IF ~~ THEN REPLY ~Business? What business is that?~ GOTO d5yxun_2
END

IF ~~ THEN BEGIN d5yxun_2
  SAY ~My business is none of your concern. I was bid to assist this endeavor by one to whom I owe a favor. That is all. And by chance, this place contains an item of some power which could be useful to me. Your involvement in this matter is irrelevant in the grand scheme of things, a mote of dust floating for a moment in a sea of time.~
  IF ~~ THEN REPLY ~You were bid here? By who?~ GOTO d5yxun_4
  IF ~!PartyHasItem("BLUN14")
  		!PartyHasItem("BLUN14D")
  		!PartyHasItem("BLUN14E")
  		!PartyHasItem("BLUN14F")
  		!PartyHasItem("BLUN14G")
  		!PartyHasItem("BLUN14H")
  		!PartyHasItem("BLUN14I")
  		!PartyHasItem("BLUN30")
  		!PartyHasItem("BLUN30D")
  		!PartyHasItem("BLUN30C")~ 
  		THEN REPLY ~What item are you talking about?~ GOTO d5yxun_5
  IF ~OR(10)
  		PartyHasItem("BLUN14")
  		PartyHasItem("BLUN14D")
  		PartyHasItem("BLUN14E")
  		PartyHasItem("BLUN14F")
  		PartyHasItem("BLUN14G")
  		PartyHasItem("BLUN14H")
  		PartyHasItem("BLUN14I")
  		PartyHasItem("BLUN30")
  		PartyHasItem("BLUN30D")
  		PartyHasItem("BLUN30C")~ 
  		THEN REPLY ~What item are you talking about?~ GOTO d5yxun_6
END

IF ~~ THEN BEGIN d5yxun_3
  SAY ~Responsible? Is the ocean responsible for the drowning of a child? I am here; and the tides of fate shift. You mortals are but insects in the path of a hurricane. If you serve to feed or amuse my minions, so be it; it matters little to my task.~
  IF ~~ THEN REPLY ~What task is that?~ GOTO d5yxun_2
END

IF ~~ THEN BEGIN d5yxun_4
  SAY ~It is... almost interesting to observe you. You with your tiny spark of possibility, and the way greater forces swirl around you, sniffing, curious. They are as mountains to you, unimaginably large. And yet you are oblivious to them. Yours is a strange fate, even if you manage to keep hold of it. I almost regret that I may extinguish it. Almost. But in the end my dealings are of far greater importance. All you need know is that I am contracted for a task, and you have placed yourself in the way of that. Thus your spark will flicker out. The great wheel will continue turning, and the truly luminous motes will continue to swirl and eddy in its wake.~
  IF ~!PartyHasItem("BLUN14")
  		!PartyHasItem("BLUN14D")
  		!PartyHasItem("BLUN14E")
  		!PartyHasItem("BLUN14F")
  		!PartyHasItem("BLUN14G")
  		!PartyHasItem("BLUN14H")
  		!PartyHasItem("BLUN14I")
  		!PartyHasItem("BLUN30")
  		!PartyHasItem("BLUN30D")
  		!PartyHasItem("BLUN30C")~ 
  		THEN REPLY ~What item were you talking about?~ GOTO d5yxun_5
  IF ~OR(10)
  		PartyHasItem("BLUN14")
  		PartyHasItem("BLUN14D")
  		PartyHasItem("BLUN14E")
  		PartyHasItem("BLUN14F")
  		PartyHasItem("BLUN14G")
  		PartyHasItem("BLUN14H")
  		PartyHasItem("BLUN14I")
  		PartyHasItem("BLUN30")
  		PartyHasItem("BLUN30D")
  		PartyHasItem("BLUN30C")~ 
  		THEN REPLY ~What item were you talking about?~ GOTO d5yxun_6
END

IF ~~ THEN BEGIN d5yxun_5
  SAY ~A tool turned weapon, very ancient, with a spark like your own. To those, like me, with the ability to unlock its nature, it can grant power over time itself. But you would never recognize or understand such nature. All you need understand is that you are between it and me, and so you will be snuffed out.~
  IF ~~ THEN REPLY ~You will gain no weapons today fiend. It is you who will be snuffed out.~ GOTO d5yxun_7
  IF ~~ THEN REPLY ~I still don't understand what your agenda is... but I am going to put a stop to it, regardless.~ GOTO d5yxun_7
  IF ~~ THEN REPLY ~I am not here to oppose whatever schemes you have. I think I will just see myself out...~ GOTO d5yxun_9
END

IF ~~ THEN BEGIN d5yxun_6
  SAY ~Foolish child! You stand there holding it, yet you cannot recognize its spark! A tool turned weapon, so ancient, with power over time and matter... but your insignificant mind could not hope to unlock its nature. All you need understand is that you are between it and me, and so you will be snuffed out.~
  IF ~~ THEN REPLY ~You will gain no weapons today fiend. It is you who will be snuffed out.~ GOTO d5yxun_8
  IF ~~ THEN REPLY ~I still don't understand what your agenda is... but I am going to put a stop to it, regardless.~ GOTO d5yxun_8
  IF ~~ THEN REPLY ~I am not here to oppose whatever schemes you have. I think I will just see myself out...~ GOTO d5yxun_9
END

IF ~~ THEN BEGIN d5yxun_7
  SAY ~Fool! You choose to dance the dance of death with one who cannot die! Pray your soul is prepared!~
  IF ~~ THEN DO ~Enemy()~ EXIT
END

IF ~~ THEN BEGIN d5yxun_8
  SAY ~I will not be denied my prize when it is so close? Now you will dance the dance of death with one who cannot die! Pray your soul is prepared!~
  IF ~~ THEN DO ~Enemy()~ EXIT
END

IF ~~ THEN BEGIN d5yxun_9
  SAY ~Oh, it is too late for that! Now you will dance the dance of death with one who cannot die! Pray your soul is prepared!~
  IF ~~ THEN DO ~Enemy()~ EXIT
END

END
>>>>>>>>
COPY ~d5/dyxun+.d~ ~weidu_external/%MOD_FOLDER%/compile/dyxun+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/dyxun+.d~

////////////////////

// set up travel triggers to severed hand

COPY_EXISTING ~AR1700.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 1 // 885  // x coordinate
    fj_box_top      = 2920 // y coordinate
    fj_box_right    = 850 // 1135 // x coordinate
    fj_box_bottom   = 3135 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 1 + (2920 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 850 + (2920 << 16)
    fj_vertex_2     = 850 + (3135 << 16)
    fj_vertex_3     = 1 + (3135 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix1700 //name of this travel region so you can address it via script etc.
    fj_destination_area = id5000 //name of the destination area
    fj_destination_name = d5id5000 //name of exitpoint in destination area
  END
BUT_ONLY

COPY_EXISTING ~ID5000.are~ ~override~
 /* add travel region that links to other area */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel - kind of trigger area
    fj_box_left     = 465 // 885  // x coordinate
    fj_box_top      = 805 // y coordinate
    fj_box_right    = 1340 // 1135 // x coordinate
    fj_box_bottom   = 895 // y coordinate
    fj_cursor_idx   = 30   //door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 465 + (805 << 16) //start with the lowest point: x coodinate - y coordinate
    fj_vertex_1     = 1340 + (805 << 16)
    fj_vertex_2     = 1340 + (895 << 16)
    fj_vertex_3     = 465 + (895 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = d5ix5000 //name of this travel region so you can address it via script etc.
    fj_destination_area = AR1700 //name of the destination area
    fj_destination_name = L-Exit //name of exitpoint in destination area
  END

/* add exitpoint for backwards travel */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 995 //x coordinate where the group will spawn
    fj_loc_y       = 810 //y coordinate where the group will spawn
    fj_orientation = 6   // NW - face direction in which group is looking after coming through
    STR_VAR
    fj_structure_type = entrance
    fj_name           = d5id5000 //name of this new exitpoint so it can be referenced for the travel region of the other area
  END
BUT_ONLY 

////////////////////

// set up scripts to make severed hand travel triggers reactive to where the player is

// use the GV set up above for Easthaven 

// also, make severed hand inaccessible without the heartstone gem

// also, more scripted changes to SH areas/actors

<<<<<<<< d5/id5000_.baf
IF
	Global("D5_IWD_REAL","GLOBAL",1)
	Global("D5_SH_IWD","ID5000",0)
THEN
	RESPONSE #100
	SetGlobal("D5_SH_IWD","ID5000",1)
	SetGlobal("D5_SH_BG2","ID5000",0)
	TriggerActivation("d5ix5000",FALSE)
END

IF
	Global("D5_EET_IWD","GLOBAL",1)
	Global("D5_SH_BG2","ID4001",0)
THEN
	RESPONSE #100
	SetGlobal("D5_SH_IWD","ID4001",0)
	SetGlobal("D5_SH_BG2","ID4001",1)
	TriggerActivation("d5ix5000",TRUE)
END

IF
	PartyHasItem("HEARTGM")  // Heartstone Gem
	Global("BGHeartstone_Active","GLOBAL",1)
	Global("D5_HAS_HGEM","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("D5_HAS_HGEM","ID5000",1)
	SetGlobal("D5_NOT_HGEM","ID5000",0)
	TriggerActivation("To5001",TRUE)
END

IF
	OR(2)
	  !PartyHasItem("HEARTGM")  // Heartstone Gem
	  GlobalLT("BGHeartstone_Active","GLOBAL",1)
	Global("D5_NOT_HGEM","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("D5_HAS_HGEM","ID5000",0)
	SetGlobal("D5_NOT_HGEM","ID5000",1)
	TriggerActivation("To5001",FALSE)
END
>>>>>>>> 
COPY ~d5/id5000_.baf~ ~weidu_external/%MOD_FOLDER%/compile/id5000_.baf~
LAF cd_extend_bg_area_script STR_VAR area = ~id5000~ script = EVAL ~weidu_external/%MOD_FOLDER%/compile/id5000_~ END

////////////////////

ACTION_IF (MOD_IS_INSTALLED ~d5_iwd_eet_integration.TP2~ ~110~) BEGIN	//	if this mod has set IWD XP to full campaign values

// adjust XP rewards back down if playing from BG2

<<<<<<<< d5/ddenain+.d
ADD_TRANS_TRIGGER DDENAINI 18 ~Global("D5_IWD_EET","GLOBAL",0)~ DO 0

EXTEND_TOP DDENAINI 18
  IF ~Global("D5_IWD_EET","GLOBAL",1)~ 
  THEN REPLY @8326 DO ~AddXP2DA("ID1EX3A")
                       DisplayStringNoNameDlg(LastTalkedToBy,@8139)
                       SetGlobal("HolyWaterGiven","GLOBAL",1)
                       SetGlobal("Restore5201","GLOBAL",1)
                       GiveItemCreate("HELMSUN",LastTalkedToBy,0,0,0)~ EXIT
  END

ADD_TRANS_TRIGGER DDENAINI 19 ~Global("D5_IWD_EET","GLOBAL",0)~ DO 0

EXTEND_TOP DDENAINI 19
  IF ~Global("D5_IWD_EET","GLOBAL",1)~ 
  THEN REPLY @8329 DO ~AddXP2DA("ID1EX3A")
                       DisplayStringNoNameDlg(LastTalkedToBy,@14413)
                       GiveItemCreate("SLINGED",LastTalkedToBy,0,0,0)~ EXIT
  END
>>>>>>>> 
  COPY ~d5/ddenain+.d~ ~weidu_external/%MOD_FOLDER%/compile/ddenain+.d~
  WITH_TRA ~iwd1_eet/lang/%LANGUAGE%/dialog.tra~ BEGIN
    COMPILE ~weidu_external/%MOD_FOLDER%/compile/ddenain+.d~
  END

/* not sure if this will work
<<<<<<<< d5/dkayles_.d
ADD_TRANS_TRIGGER DKAYLESS 15 ~Global("D5_IWD_EET","GLOBAL",0)~ DO 0

EXTEND_TOP DKAYLESS 15
  IF ~Global("D5_IWD_EET","GLOBAL",1)
      GlobalGT("SPRITE_IS_DEADSH_Warrior_Dead","GLOBAL",24)
      Global("K_Done","GLOBAL",0)~ THEN REPLY @8399 DO ~AddXP2DA("ID1EX3H")
                                                        DisplayStringNoNameDlg(LastTalkedToBy,@8142)
                                                        SetGlobal("K_Done","GLOBAL",1)
                                                        EraseJournalEntry(@14416)
                                                        AddJournalEntry(@14415,QUEST_DONE)~ GOTO 18
  END

ADD_TRANS_TRIGGER DKAYLESS 17 ~Global("D5_IWD_EET","GLOBAL",0)~ DO 0

EXTEND_TOP DKAYLESS 17
  IF ~Global("D5_IWD_EET","GLOBAL",1)
      GlobalGT("SPRITE_IS_DEADSH_Warrior_Dead","GLOBAL",24)
      Global("KaylessaQuest","GLOBAL",1)
      Global("K_Done","GLOBAL",0)~ THEN REPLY @8396 DO ~AddXP2DA("ID1EX3H")
                                                        DisplayStringNoNameDlg(LastTalkedToBy,@8142)
                                                        SetGlobal("K_Done","GLOBAL",1)
                                                        EraseJournalEntry(@14416)
                                                        AddJournalEntry(@14417,QUEST_DONE)~ GOTO 18
  END
>>>>>>>> 
  COPY ~d5/dkayles+.d~ ~weidu_external/%MOD_FOLDER%/compile/dkayles+.d~
  COMPILE ~weidu_external/%MOD_FOLDER%/compile/dkayles+.d~
*/
  WITH_TRA ~iwd1_eet/lang/%LANGUAGE%/dialog.tra~ BEGIN
    COMPILE ~%MOD_FOLDER%/data/compile/dkayless.d~
  END

// don't do quest XP in dragon's eye unless playing from IWD

  COPY_EXISTING ~id4005.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY
~Global("YXUNOMEI_SLAIN","GLOBAL",0)~ 
~Global("YXUNOMEI_SLAIN","GLOBAL",0)
    Global("D5_IWD_REAL","GLOBAL",1)~
      REPLACE_TEXTUALLY
~Global("Kuldahar_Attack","GLOBAL",0)~
~Global("Kuldahar_Attack","GLOBAL",0)
    Global("D5_IWD_REAL","GLOBAL",1)~
    END

/* no, already removed access to presio's level
// adjust presio XP reward in id4003.bcs as well

  COPY ~%MOD_FOLDER%/data/copy/id4003.bcs~ ~override~
*/

// and adjust the reward from villagers in ID4001

  COPY ~%MOD_FOLDER%/data/copy/dcapvil3.dlg~ ~override~

END	//	end if iwd xp was restored

////////////////////

// change larrel's dialogue to activate gem and teleport party to athkatla, no cut-scene

// ***** probably want to play some kind of visual effect when he activates it

<<<<<<<< d5/dlarrel+.d
APPEND ~dlarrel~
  IF ~~ THEN BEGIN ~d5bglarrel1~
    SAY ~What is it you seek to learn from the Heartstone Gem?~
    IF ~~ THEN REPLY ~We seek to activate the Gem in order to learn the location of my friend, Imoen.~  GOTO d5bglarrel2
    IF ~~ THEN REPLY ~We seek to activate the Gem in order to learn the location of the mage Jon Irenicus~  GOTO d5bglarrel2
  END

  IF ~~ THEN BEGIN ~d5bglarrel2~
  SAY ~Then you will have what you seek. I will activate the Heartstone Gem, and then it may be used to pierce even the most powerful magical concealment.~
  IF ~~ THEN REPLY ~Here you are.~  DO ~SetGlobal("BGHeartstone_Active","GLOBAL",2) DisplayStringNoNameDlg(LastTalkedToBy,499613)~ EXIT
  END
END

EXTEND_BOTTOM DLARREL 15 
  IF ~	Global("D5_EET_IWD","GLOBAL",1)
      PartyHasItem("HEARTGM")~  THEN GOTO ~d5bglarrel1~
END
>>>>>>>> 
COPY ~d5/dlarrel+.d~ ~weidu_external/%MOD_FOLDER%/compile/dlarrel+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/dlarrel+.d~

// ***** should apply some log text / visual effect to this
// ...maybe actually swap out the gem for a new .ITM with a new description?

////////////////////

// change gaelan's dialogue to send player to severed hand, and accept activated heartstone gem

<<<<<<<< d5/gaelan+.d
ADD_STATE_TRIGGER GAELAN 51 ~GlobalLT("BGHeartstone_Active","GLOBAL",2)~

EXTEND_BOTTOM GAELAN 49
  IF ~Global("BGHeartstone_Active","GLOBAL",2)~ THEN REPLY ~I have activated the Heartstone Gem. You may have it instead of the fee.~
    DO ~ EraseJournalEntry(96385)
      AddJournalEntry(96385,QUEST_DONE)
      AddJournalEntry(96437,QUEST)
      EraseJournalEntry(34183)
      EraseJournalEntry(34187)
      AddXPObject(Player1,45000)
      AddXPObject(Player2,45000)
      AddXPObject(Player3,45000)
      AddXPObject(Player4,45000)
      AddXPObject(Player5,45000)
      AddXPObject(Player6,45000)
      SetGlobal("WorkingForAran","GLOBAL",1) ~ SOLVED_JOURNAL #47537 /* ~Collect the fee for Gaelan Bayle

I have given Gaelan Bayle the gold he asked for, and in return he has given me a key which will admit me to see the head of the Athkatlan Shadow Thieves. I am to go to the thieves' guild, it seems, and use the key on a secret entrance which is immediately to the right as I enter. ~ */ GOTO 53
END

APPEND GAELAN
  IF WEIGHT #9 ~ Global("Linvail","GLOBAL",0)
    Global("ShadowWork","GLOBAL",1)
    PartyHasItem("HEARTGM")
    Global("BGHeartstone_Active","GLOBAL",0)
    Global("BodiWork","GLOBAL",0) ~ 
  THEN BEGIN ~d5hggaelan1~
    SAY ~Coo! A little birdy told me ye've come into possession of an artifact that can do powerful divination. Though the use of it be tricky for most folks, we have someone who can use it to assist in reaching Imoen and Irenicus. In fact, we would so appreciate having the gem that, if ye can part with it, we can forego our fee entirely. And to be honest, ye haven't the ability to use it, so it's worth nothing to ye. What say ye?~
    IF ~~ THEN REPLY ~Sure, sounds good. I guess I have no use for the gem anyway.~  GOTO d5hggaelan2
    IF ~ PartyGoldLT(15000) ~ THEN REPLY ~Uh, I'm not sure I like the idea of the Shadow Thieves having an artifact of such power. No offense. I'll pay the fee instead.~  GOTO 49
    IF ~ PartyGoldGT(14999) ~ THEN REPLY ~Uh, I'm not sure I like the idea of the Shadow Thieves having an artifact of such power. No offense. I'll pay the fee instead.~  GOTO 51
  END

  IF ~~ THEN BEGIN ~d5hggaelan2~
    SAY ~Ah, it be as I feared: the gem is inactive. It needs to be woken up, ye might say. Luckily our contact was prepared for this possibility, and told me how it can be done. Ye will have to find an elven lich who has had experience with the gem in the distant past. Don't worry, he be friendly. I think. What do ye think? Are ye up for it?~
    IF ~~ THEN REPLY ~If it helps get us to our goal, I will find this lich and activate the gem.~ GOTO d5hggaelan3
    IF ~ PartyGoldLT(15000) ~ THEN REPLY ~On second thought, let's keep things simple. I'll pay your fee instead.~  GOTO 49
    IF ~ PartyGoldGT(14999) ~ THEN REPLY ~On second thought, let's keep things simple. I'll pay your fee instead.~  GOTO 51
  END

  IF ~~ THEN BEGIN ~d5hggaelan3~
    SAY ~Coo! I half didn't expect ye to show the stones for it. Very well then, ye be wanting to find a ruined elven fortress, far t'other side of Small Teeth Pass. I hear 'twas once called 'Hand of the Seldarine,' tho it be fairly severed now. The gem should gain ye entry, but I know not what lurks within. Best of luck to ye!~
    IF ~~ THEN REPLY ~Very well.~ DO ~SetGlobal("BGHeartstone_Active","GLOBAL",1) RevealAreaOnMap("AR1700")~ EXIT
  END

  IF WEIGHT #10 ~Global("BGHeartstone_Active","GLOBAL",2)~ THEN BEGIN ~d5hggaelan4~
  SAY ~Ye survived! Astounding. I mean, wonderful! Now, is our deal still good? Will ye turn over the gem to me?~
  IF ~~ THEN REPLY #21033 /* ~Yes, Here you go.~ */ 
    DO ~ EraseJournalEntry(96385)
      AddJournalEntry(96385,QUEST_DONE)
      AddJournalEntry(96437,QUEST)
      EraseJournalEntry(34183)
      EraseJournalEntry(34187)
      AddXPObject(Player1,45000)
      AddXPObject(Player2,45000)
      AddXPObject(Player3,45000)
      AddXPObject(Player4,45000)
      AddXPObject(Player5,45000)
      AddXPObject(Player6,45000)
      SetGlobal("WorkingForAran","GLOBAL",1) ~ SOLVED_JOURNAL #47537 /* ~Collect the fee for Gaelan Bayle

I have given Gaelan Bayle the gold he asked for, and in return he has given me a key which will admit me to see the head of the Athkatlan Shadow Thieves. I am to go to the thieves' guild, it seems, and use the key on a secret entrance which is immediately to the right as I enter. ~ */ GOTO 53
    IF ~ PartyGoldLT(15000) ~ THEN REPLY ~On second thought, I'm not sure I like the idea of the Shadow Thieves having an artifact of such power. No offense. I'll pay the fee instead.~  GOTO 49
    IF ~ PartyGoldGT(14999) ~ THEN REPLY ~On second thought, I'm not sure I like the idea of the Shadow Thieves having an artifact of such power. No offense. I'll pay the fee instead.~  GOTO 84
  END
END
>>>>>>>> 
COPY ~d5/gaelan+.d~ ~weidu_external/%MOD_FOLDER%/compile/gaelan+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/gaelan+.d~

